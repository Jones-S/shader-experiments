<!DOCTYPE html>
<html lang="">
<head>
  <meta charset="utf-8">
    <title>Shader Experiments</title>
    <meta name="description" content="" />
    <meta name="author" content="">
    <meta name="keywords" content="" />
    <meta name="robots" content="" />
    <meta name="viewport" content="width=device-width, initial-scale=1" />
    <!-- <link rel="stylesheet" type="text/css" href="css/main.css" /> -->
    <!-- Shaders -->

</head>
    <body>

    	<div id="container">


    	</div>
        <script type="x-shader/x-vertex" id="vertexshader">

            attribute vec2 uv2;
            // Examples of variables passed from vertex to fragment shader
            varying vec2 vUv;

            void main() {

               // To pass variables to the fragment shader, you assign them here in the
               // main function. Traditionally you name the varying with vAttributeName
               vUv = uv;

               // This sets the position of the vertex in 3d space. The correct math is
               // provided below to take into account camera and object data.
               gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );

            }
        </script>

        <script type="x-shader/x-fragment" id="fragmentshader">
            #define tau 6.2831853

            uniform float time;
            uniform float contrast;
            uniform float distortion;
            uniform float speed;
            uniform vec3 color;
            uniform float brightness;
            uniform sampler2D noiseImage;
            uniform vec2 resolution;

            varying vec2 vUv;

            mat2 makeMatrix2(in float theta) {
                float c = cos(theta);
                float s = sin(theta);
                return mat2(c, -s, s, c);
            }

            float noise(in vec2 x) {
                return texture2D(noiseImage, x * .01).x; //.x taking only the x coordinate
            }

            // what is a fractal Brownian Motion:
            // Read this: https://thebookofshaders.com/13/
            float fractalBrownianMotion(in vec2 p) {
                float amplitude = 2.0;
                float value = 0.0;
                vec2 bp = p;
                const float OCTAVES = 6.0; // needs to be a constant float...

                for (float i = 1.; i < OCTAVES; i++) {
                    // because the noise is signed (only positive values) this is called turbulence and not fBM anymore
                    value += abs((noise(p) - 0.5) * 2.0) / amplitude;
                    amplitude = amplitude * 2.0;
                    p = p * 2.0;
                }
                return value;
            }

            float dualFractalBrownianMotion(in vec2 p) {
                vec2 p2 = p * distortion;
                vec2 basis = vec2(
                    fractalBrownianMotion(p2 - time * speed * 1.6),
                    fractalBrownianMotion(p2 + time * speed * 1.7));
                basis = (basis - .5) * .2;
                p += basis;
                return fractalBrownianMotion(p * makeMatrix2(time *  speed * 0.2));
            }

            void main() {
                vec2 point = (vUv.xy) * resolution; //take the x and y coordinate an multiply with the resolution
                float rz = dualFractalBrownianMotion(point);

                vec3 col = (color / rz) * brightness;

                col = ((col - 0.5) * max(contrast, 0.0)) + 0.5;

                gl_FragColor = vec4(col, 1.0);
            }

        </script>

        <!-- End Shaders -->
        <script src="https://ajax.googleapis.com/ajax/libs/jquery/1.5.1/jquery.min.js"></script>
        <script src="../node_modules/three/build/three.js"></script>
        <script src="js/shader.js"></script>
    </body>



</html>